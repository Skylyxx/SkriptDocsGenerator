import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group 'fr.skylyxx.docsgenerator'
version '1.1'

def mcVersion = '1.16'
def mcSubVersion = '.4'
def devOutput = 'D:\\Servers\\Minecraft\\SkriptDocsGenerator\\plugins'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots'}
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation(group: 'org.jetbrains', name: 'annotations', version: '16.0.2')
    implementation(group: 'org.spigotmc', name: 'spigot-api', version: mcVersion + mcSubVersion + '-R0.1-SNAPSHOT')
    implementation('com.github.SkriptLang:Skript:2.5.3') {
        exclude group: 'com.sk89q.worldguard', module: 'worldguard-legacy'
        exclude group: 'net.milkbowl.vault', module: 'Vault'
        exclude group: 'com.destroystokyo.paper', module: 'paper-api'
        exclude group: 'io.papermc', module: 'paperlib'
    }
}

jar {
    archiveName("SkriptDocsGenerator.jar")
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

task githubRessources(type: ProcessResources) {
    from("src/main/resources", {
        include("**")
        def lastCommit = getGitHash()
        version = project.version + "-Github-" + lastCommit
        filter ReplaceTokens, tokens: [
                'version': version,
                'apiVersion': mcVersion,
        ]
    })
    into("build/resources/main")
}

task githubRelease(type: Jar) {
    from sourceSets.main.output
    dependsOn(githubRessources)
    archiveName("SkriptDocsGenerator-Github.jar")
}

processResources {
    filter ReplaceTokens, tokens: [
            'version': project.property('version'),
            'apiVersion': mcVersion,
    ]
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'